/**
 * =====================================
 * ErrorFound ( errorData )
 *
 * RETURNS:
 *		Boolean true for anything other than 0 and empty. In other words,
 *		if any error occurred at all, return True (1), otherwise return
 *		False (0).
 *
 * PARAMETERS:
 *		errorData = (JSON) output of Error custom function instantiated as $error. Can also be a numerical error code.
 *
 * DEPENDENCIES:
 *		None
 *
 * HISTORY:
 *		MODIFIED on 2023-DEC-23 by Robert Naud robert@accoladeplusaccolade.com
 *			- Updated for implementation of reserved $error variable.
 *		MODIFIED on 2022-AUG-10 by Matt Petrowsky editor@filemakermagazine.com
 *			- Validating JSON as GetAsBoolean would take any positive value.
 *		MODIFIED on 2022-AUG-01 by Matt Petrowsky editor@filemakermagazine.com
 *			- Refactored to use JSON.
 *		MODIFIED on 2013-NOV-22 by Daniel Smith dansmith65@gmail.com
 *			- renamed to ErrorFound, update documentation
 *		MODIFIED on 2013-OCT-11 by Daniel Smith dansmith65@gmail.com
 *			- renamed from IsError to #IsError
 *			- update documentation in script header
 *		MODIFIED on 2012-NOV-21 by Daniel Smith dansmith65@gmail.com
 *			- don't access $error variable directly
 *		CREATED on 2012-NOV-20 Daniel Smith dansmith65@gmail.com
 *
 * REFERENCES:
 *		https://github.com/filemakerstandards/fmpstandards/tree/master/Functions/Errors
 * =====================================
 */

Let ( [
	~jsonCode = JSONGetElement ( errorData ; "code" );
	~jsonType = JSONGetElementType ( errorData ; "code" )
];
	Case ( 
		IsEmpty ( errorData ) ;
			False; 
	
		Exact ( errorData ; Filter ( errorData ; "-0123456789" ) ) ;
			GetAsBoolean ( errorData );
	
		~jsonType = JSONNumber ;
			GetAsBoolean ( ~jsonCode );
	
		Left ( ~jsonType ; 1 ) = "?" ;
			GetAsBoolean ( ~jsonCode );
	
		/*default result*/ 
			True
	)
)

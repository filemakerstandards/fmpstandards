/*
 * =====================================================
 * WindowActiveTabs ()
 *		https://github.com/petrowsky/fmpstandards/blob/master/Functions/WindowActiveTabs.fmfn
 * 
 * PARAMETERS: none
 * RETURNS:
 *		(list) List of all active frontmost tabs
 * DEPENDENCIES:
 *		none
 * NOTES:
 *		Original by Koen Van Hulle (SHpartners.com)
 * =====================================================
 */

Let ( [

	$~i = $~i +1; // increment counter
	$~objects = LayoutObjectNames ( Get( FileName ); Get ( LayoutName ) );
	~tabToCheck = GetValue ( $~objects ; $~i );
	~parentTab = GetLayoutObjectAttribute ( ~tabToCheck; "enclosingObject" );
	~isFront = GetLayoutObjectAttribute ( ~tabToCheck ; "isFrontTabPanel" );
	~isCurrentTab = Case(
						IsEmpty ( ~tabToCheck ); // there's no tab name
							False;

						~isFront
						and IsEmpty ( ~parentTab ); // does not have a parent
							True;
							
						~isFront
						and PatternCount ( ¶ & $~tabList & ¶; ¶ & ~parentTab & ¶ ); // parent is already in $~tabList
							True;
							
						False
					);
	$~tabList = List ( $~tabList ; If ( ~isCurrentTab ; ~tabToCheck ) )

];

	If ( not IsEmpty ( ~tabToCheck );
		WindowActiveTabs;
	/*Else*/
		$~tabList
		& Let ( [
			// purge variables
			$~i = "";
			$~objects = "";
			$~tabList = ""
		];
			"" // null
		)
	) 
	
)
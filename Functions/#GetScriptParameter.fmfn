/**
 * =====================================
 * #GetScriptParameter ( name )
 *
 * PURPOSE:
 * Retrieves values from a name-value pair list created with the
 * # ( name ; value ) function in the script parameter. This function does not
 * set any variables, making it non-destructive, in contrast with the
 * #AssignScriptParameters function, which can over-write variables. This
 * function is equivalent to #Get ( Get ( ScriptParameter ) ; name ).
 *
 * RETURNS:
 * The value from the name-value pair specified by the name parameter. If such a
 * value is not in the script parameter, the function returns Null ("").
 *
 * PARAMETERS:
 * name: The name of the name-value pair from which to retrieve the value.
 *
 * DEPENDENCIES: none
 *
 * HISTORY:
 * CREATED on 2011-01-14 by Jeremy Bante of The Support Group
 * (jbante@supportgroup.com).
 *
 * REFERENCES:
 * Script Parameter Interface Best Practice: http://filemakerstandards.org/pages/viewpage.action?pageId=557462
 * =====================================
 */

Let ( [
	~name = "¶$" & name & " = ";
	~parameters = Get ( ScriptParameter );
	~namePosition = Position ( "¶" & ~parameters ; ~name ; 1 ; 1	);
	~valuePosition = ~namePosition + Length ( ~name ) - 1;
	~endPosition = Position ( ~parameters ; ";¶" ; ~namePosition ; 1 );
	~value =
		Middle (
			~parameters;
			~valuePosition;
			~endPosition - ~valuePosition
		);
	~error = not ~namePosition or EvaluationError ( ~value )
];
	If ( ~error ; "" ; /*else*/ Evaluate ( ~value ) )
)
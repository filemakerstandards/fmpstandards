/**
 * =====================================
 * TriggersEnable
 *
 * PURPOSE:
 * TriggersEnable sets global variables to indicate that the current script is
 * no longer suppressing triggers. This function will also turn off trigger
 * suppression altogether if there are no other scripts suppressing triggers. In
 * order to be suppressed, a script called via trigger should use the
 * TriggersAreActive function to decide whether or not to run.
 *
 * RETURNS:
 * True (1) if TriggersEnable successfully removed the current script from
 * the list of scripts suppressing triggers; False (0) otherwise.
 *
 * PARAMETERS: none
 *
 * REFERENCED VARIABLES:
 * $$~DISABLETRIGGERS
 * $$~DISABLETRIGGERS.SCRIPTS
 *
 * DEPENDENCIES: none
 *
 * EXAMPLE (script):
 * Set Variable [ $ignoreMe ; TriggersDisable ]
 *        # Do something
 * Set Variable [ $ignoreMe ; TriggersEnable ]
 *
 * NOTES:
 * This function should *always* be used in pairs with the TriggersDisable
 * function.
 *
 * HISTORY:
 * MODIFIED on 2010-12-13 by jbante@supportgroup.com to use names suggested at
 * filemakerstandards.org.
 * CREATED on 2010-12-12 by Jeremy Bante of The Support Group (jbante@supportgroup.com).
 *
 * REFERENCES:
 * Supressible Triggered Scripts Best Practice: http://filemakerstandards.org/display/bp/Suppressible+Triggered+Scripts
 * =====================================
 */

Let ( [
        ~lastScript = GetValue ( $$~DISABLETRIGGERS.SCRIPTS ; 1 );
        ~enableTriggers = Get ( ScriptName ) = ~lastScript; //boolean indicating script name match
        $$~DISABLETRIGGERS.SCRIPTS =
                If ( ~enableTriggers;
                        RightValues (
                        	$$~DISABLETRIGGERS.SCRIPTS;
                        	ValueCount ( $$~DISABLETRIGGERS.SCRIPTS ) - 1
                        );
                        /*else*/ $$~DISABLETRIGGERS.SCRIPTS
                );
        ~scriptCount = ValueCount ( $$~DISABLETRIGGERS.SCRIPTS );
        $$~DISABLETRIGGERS = If ( ~scriptCount ; True ; /*else*/ "" )
];
        ~enableTriggers or not ~scriptCount //ScriptName was found and removed, or there are no suppressing scripts
)